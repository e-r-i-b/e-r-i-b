// ══════════════════════════════════════
// ──────────────────────────────────────
// ИКФЛ. Главный макрос 
// ──────────────────────────────────────
// ══════════════════════════════════════


import rcw, rsd, deprintr;
import "rt_ib_xml.mac"; 
import "rt_ib_getDepositList.mac",
       "rt_ib_DepositProduct.mac",
       "rt_ib_getCurrencyRate.mac",  
       "rt_ib_getClientDepositInfo.mac",
       "rt_ib_CalcComPay.mac",
       "rt_ib_makePostDocument.mac",
       "rt_ib_carryDeposit.mac", 
       "rt_ib_Calendar.mac",
       "rt_ib_recallDocument.mac"; 

//для выписки
import "rt_ib_extract.mac";

// ───────────────────────────────────────
// Обработчик 
// ───────────────────────────────────────
macro requestDispatcher( xmlDoc, outXml )

  var firstNode, nextNode;
  var proc_ref = "";

  outXml = null;

  firstNode = xmlDoc.documentElement.childNodes.item(0);   
  if ( firstNode.NodeName == "id" )
    ParentId = firstNode.text;
  end;

  nextNode = firstNode.nextSibling;
  if ( nextNode )
    NameInputTag = nextNode.nodeName;
  end;

  if  ( NameInputTag == "getDepositList_q" )
    proc_ref = "GetDepositList";    // 
  elif( NameInputTag == "depositProduct_q" )
    proc_ref = "DepositProduct";    // 
  elif( NameInputTag == "getClientDepositInfo_q" )
    proc_ref = "GetClientDepositInfo";
  elif ( NameInputTag == "getCurrencyRate_q" )
    proc_ref = "getCurrencyRate";  // курс валюты

  elif ( NameInputTag == "getAccountTransactions_q" )
    //proc_ref = "getAccTransactions";  // выписки
    proc_ref = "Extract";  // выписки

  elif( NameInputTag == "commissionPay_q" )
    proc_ref = "CalcComPay";       // комиссия вкладного документа
  elif(( NameInputTag == "internalTransfer_q" ) or
       ( NameInputTag == "openDeposit_q"      ) or
       ( NameInputTag == "closeDeposit_q"     ) or
       ( NameInputTag == "externalTransfer_q" ) 
      )
    proc_ref = "makePayDocument";       // комиссия вкладного документа
  elif( NameInputTag == "carryDeposit_q" )
    proc_ref = "carryDeposit";       // комиссия вкладного документа
  elif( NameInputTag == "recallDocument_q" )
    proc_ref = "recallDocument";       // Отзыв платежа
  elif ( NameInputTag == "nextWorkDate_q" )
    proc_ref = "GetNextWorkDate";     // Получить следующий рабочий день
  else
    outXml = AddError2OutXml_(outXML, "неизвестный запрос");
  end;

  if( proc_ref )
    if(not ExecMacro(proc_ref, xmlDoc, outXml))
      outXml = AddError2OutXml_(outXML, "Ошибка запуска процедуры "+proc_ref+" rt_ib__mail.mac" );
    end;
  end;
  SetParm( 1, outXml );

end;


// ═══════════════════════════════════════
// Головная функция
// ═══════════════════════════════════════
macro XML_Dispatch( xml_request )
  var xmlDoc, outXml, strErr = "ERROR";
  var account, filial, dateDoc, sum, currency, clientID, operationType, operationSubspecies;

  xmlDoc = CreateXMLObject();

  if( xmlDoc )
    if( xmlDoc.loadXML(xml_request) )
      if( xmlDoc.documentElement.nodeName == "request" )
        requestDispatcher( xmlDoc, outXml );
      else
        // другие действия :)
        outXml = AddError2OutXml_(outXML, "Запрос '"+xmlDoc.documentElement.nodeName+"' не поддерживается" );
      end;
    else
      outXml = AddError2OutXml_(outXML, "Ошибка загрузки xml-документа" );
    end;
  else
    strErr = "Ошибка инициализации Active-X компонента";
  end;

  if( outXml )
    return "<?xml version=\"1.0\" encoding=\"CP866\"?>"+string(outXml.xml);
  else
    return (strErr);
  end;

end;




// ───────────────────────────────────────
// Отладка
// ───────────────────────────────────────              
macro debug
  var request = "";
  FILE t("C:\\RsBank60.20.29\\retail_deb\\_info.xml") txt; 

  if ( open(t) )
    while(next(t) )
      if ( substr( t.str, 1, 1 ) != ";" )
        request = request + t.str;
      end;
    end;
    close(t);

    println(XML_Dispatch(request));
  
  end;
end;

